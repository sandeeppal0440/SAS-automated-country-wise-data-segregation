%macro uber();

/* ---------- Redirect LOG to file ---------- */
  proc printto 
    log="C:\Documents and Settings\Administrator\Desktop\1st_project\uber_log_%sysfunc(today(),monyy5.).log"
    new;
  run;

/* ---------- Import Excel ---------- */
proc import datafile="C:\Documents and Settings\Administrator\Desktop\1st_project\project_uber_ctry_seggregation.xls"
    out=uber_country
    dbms=xls
    replace;
	getnames=yes;   /* First row = variable names */
run;

/* ----------Create year-month macro variable---------- */
data _null_;
  call symputx('year_name', put(intnx('month', today(), -1), monyy5.));
run;

%put &=year_name;

/* ----------Get distinct country list---------- */
proc sql noprint;
  select distinct country
  into :ctry1-:ctry999
  from uber_country;
  %let total_ctry = &sqlobs;
quit;

%put Total Countries = &total_ctry;

/* ----------Create country-wise datasets---------- */
%local i;
%do i = 1 %to &total_ctry;

  %let ctry_clean =
    %sysfunc(compress(&&ctry&i., , kad));

%put &ctry_clean.;

  data uber_&year_name._&ctry_clean;
    set uber_country;
    where country = "&&ctry&i.";
  run;

/*  ----------To export all the datasets----------*/
  proc export
    data=uber_&year_name._&ctry_clean.
    outfile="C:\Documents and Settings\Administrator\Desktop\1st_project\uber_&year_name._&ctry_clean..csv"
    dbms=csv
    replace;
  run;

%end;
%mend;

%uber();
